apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

allprojects {
    repositories {
        jcenter()
        google()
    }
}

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'com.teester.whatsnearby'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 20
        versionName '0.20'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        debug {
            testCoverageEnabled = true
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.isDebuggable()) {
                //set properties for debug
                output.versionNameOverride = getVersionName()
            } else {
                //set properties for release
                output.versionNameOverride = "0.20"
                variant.outputs.all {
                    outputFileName = "whatsnearby-" + output.versionNameOverride + "-release.apk"
                }
            }
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'
    implementation 'com.mapzen.android:lost:3.0.4'
    implementation('de.westnordost:osmapi:3.3') {
        // it's already included in Android
        exclude group: 'net.sf.kxml', module: 'kxml2'
    }
    implementation 'oauth.signpost:signpost-core:1.2.1.2'
    implementation 'com.google.code.gson:gson:2.8.5'

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'org.json:json:20180813'
}


